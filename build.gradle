import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'com.yakovliam'
version '1.0.0-SNAPSHOT'
project.ext.name = 'EcoporiumScreenAddon'
project.ext.groupId = 'com.yakovliam.ecoporiumscreenaddon'

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url = 'https://repo.incendo.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    maven { url = "https://repo.mattstudios.me/artifactory/public/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = 'https://libraries.minecraft.net/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://jitpack.io' }
    maven { url = "https://repo.triumphteam.dev/snapshots" }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'

    compileOnly "org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.1"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

    implementation "net.kyori:adventure-api:4.9.3"
    implementation "net.kyori:adventure-platform-bukkit:4.0.1"
    implementation "net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT"
    implementation "me.lucko:commodore:1.11"
    implementation "dev.triumphteam:triumph-gui:3.1.1"
    implementation "co.aikar:acf-bukkit:0.5.0-SNAPSHOT"
    implementation "me.mattstudios:triumph-msg-adventure:2.2.4-SNAPSHOT"
    implementation "me.mattstudios:triumph-msg:2.2.4-SNAPSHOT"
    implementation "org.spongepowered:configurate-gson:4.1.2"

    implementation "org.jfree:jfreechart:1.5.3"

    implementation files('libs/spigot-maps-2.1.1.jar')

    compileOnly fileTree(dir: 'libs', include: 'ecoporium-api-*.jar')
}

test {
    useJUnitPlatform()
}

// Output to build/libs/shadow.jar
shadowJar {
    archiveBaseName.set("${project.ext.name}-${project.version}")
    archiveClassifier.set("")
    archiveVersion.set("")

    dependencies {
        exclude(dependency("com.mojang:brigadier"))
        exclude "*.md"
        exclude "*.txt"
    }

    relocate "dev.triumphteam.gui", "${project.ext.groupId}.lib.triumphgui"
    relocate "me.mattstudios", "${project.ext.groupId}.lib.mattstudios"
    relocate "net.kyori", "${project.ext.groupId}.lib.kyori"
    relocate "com.fasterxml.jackson", "${project.ext.groupId}.lib.yahoofinance.jackson"
    relocate "me.lucko.commodore", "${project.ext.groupId}.lib.commodore"

    relocate "co.aikar.commands.lib.timings", "${project.ext.groupId}.lib.aikar.timings"
    relocate "co.aikar.commands", "${project.ext.groupId}.lib.aikar.acf"
    relocate "co.aikar.locales", "${project.ext.groupId}.lib.aikar.locales"

    relocate "org.jfree", "${project.ext.groupId}.lib.jfree"
    relocate "org.spongepowered", "${project.ext.groupId}.lib.spongepowered"
    relocate "org.slf4j", "${project.ext.groupId}.lib.slf4j"
    relocate "org.checkerframework", "${project.ext.groupId}.lib.checkerframework"
    relocate "io.leangen", "${project.ext.groupId}.lib.leangen"
}

// Replace version information in src/main/resources
processResources {
    filter(ReplaceTokens, tokens: [version: version])
    filter(ReplaceTokens, tokens: [name: project.ext.name])
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}